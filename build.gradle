plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'org.springframework.boot' version '2.3.7.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
}

version = System.getenv('VERSION') ?: 'unspecified'

ext {
    jaegerVersion = '3.2.2'
    h2Version = '2.2.220'
    micrometerVersion = '1.7.0'
    chaosMonkeyVersion = '2.5.0'
    postgresqlVersion = '42.2.18'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencyLocking {
    lockFile = file("$projectDir/gradle.lockfile")
}

configurations {
    compileClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
    runtimeClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
    annotationProcessor {
        resolutionStrategy.activateDependencyLocking()
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.cloud:spring-cloud-starter-vault-config'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    implementation "org.postgresql:postgresql:${postgresqlVersion}"
    implementation "io.opentracing.contrib:opentracing-spring-jaeger-web-starter:${jaegerVersion}"
    implementation "io.opentracing.contrib:opentracing-spring-jaeger-cloud-starter:${jaegerVersion}"
    implementation "com.h2database:h2:${h2Version}"
    implementation "de.codecentric:chaos-monkey-spring-boot:${chaosMonkeyVersion}"
    implementation "io.micrometer:micrometer-registry-prometheus:${micrometerVersion}"

    testImplementation 'junit:junit'
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Hoxton.SR9'
    }
}

bootJar {
    archiveBaseName = 'spring-boot-payments'
    archiveVersion = ''

    manifest {
        attributes(
            'Implementation-Title': archiveBaseName,
            'Implementation-Version': version
        )
    }
}